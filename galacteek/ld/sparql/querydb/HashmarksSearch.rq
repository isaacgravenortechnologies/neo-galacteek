PREFIX gs: <ips://galacteek.ld/>
PREFIX h: <ips://galacteek.ld/Hashmark#>
PREFIX rprefs: <ips://galacteek.ld/ResourcePreferences#>

SELECT ?uri ?category ?descr ?title
       ?comment
       ?schemePreferred
       ?iconUrl ?icon
       ?mimeType ?mimeCategory ?dateCreated
       ?prefsWebProfile
       ?prefsSchemePreferred
       ?prefsInQuickAccessDock
WHERE {
  ?uri a gs:Hashmark .

  OPTIONAL { ?uri h:dateCreated ?dateCreated . } .

  ?uri h:title ?title .

  OPTIONAL {
    ?uri h:description ?descr .
    FILTER (langMatches(lang(?descr), str(?langTagMatch)) )
  } .

  OPTIONAL { ?uri h:comment ?comment . } .
  OPTIONAL { ?uri h:category ?category . } .
  OPTIONAL { ?uri h:mimeType ?mimeType . } .
  OPTIONAL { ?uri h:mimeCategory ?mimeCategory . } .
  OPTIONAL { ?uri h:schemePreferred ?schemePreferred . } .
  OPTIONAL { ?uri gs:url ?url . } .

  # Icon
  OPTIONAL { 
    ?uri gs:icon ?icon .
    ?icon gs:contentUrl ?iconUrl .
  } .

  # Resource preferences
  OPTIONAL { 
    ?uri h:resourcePrefs ?prefs .
    ?prefs rprefs:useWebProfile ?prefsWebProfile .
    ?prefs rprefs:schemePreferred ?prefsSchemePreferred .
    ?prefs rprefs:showInQuickAccess ?prefsShowInQuickAccess .
    ?prefs rprefs:inQuickAccessDock ?prefsInQuickAccessDock .
  } .

  # Thumbnail
  OPTIONAL { 
    ?uri gs:thumbnail ?icon .
    ?icon gs:contentUrl ?thumbnailUrl .
  } .

  # Filter on title and uri
  FILTER (regex(str(?title), str(?searchQuery), "i") ||
      regex(str(?descr), str(?searchQuery), "i") ||
      regex(str(?uri), str(?searchQuery), "i"))

  FILTER (langMatches(lang(?title), "*") )
}

ORDER BY DESC(?dateCreated)
