PREFIX gs: <ips://galacteek.ld/>
PREFIX h: <ips://galacteek.ld/Hashmark#>
PREFIX rprefs: <ips://galacteek.ld/ResourcePreferences#>

SELECT ?uri ?category ?descr ?title
       ?comment
       ?schemePreferred
       ?iconUrl ?icon
       ?mimeType ?mimeCategory ?dateCreated
       ?prefsWebProfile
       ?prefsSchemePreferred
       ?prefsInQuickAccessDock
WHERE {
  ?uri a gs:Hashmark .

  OPTIONAL { ?uri h:dateCreated ?dateCreated . } .

  ?uri h:title ?title .
  ?uri h:description ?descr .

  OPTIONAL { ?uri h:comment ?comment . } .
  OPTIONAL { ?uri h:category ?category . } .
  OPTIONAL { ?uri h:mimeType ?mimeType . } .
  OPTIONAL { ?uri h:mimeCategory ?mimeCategory . } .
  OPTIONAL { ?uri h:schemePreferred ?schemePreferred . } .
  OPTIONAL { ?uri gs:url ?url . } .

  # Icon
  OPTIONAL { 
    ?uri gs:icon ?icon .
    ?icon gs:contentUrl ?iconUrl .
  } .

  # Resource preferences
  ?uri h:resourcePrefs ?prefs .
  ?prefs rprefs:inQuickAccessDock ?prefsInQuickAccessDock .

  OPTIONAL {
    ?prefs rprefs:useWebProfile ?prefsWebProfile .
    ?prefs rprefs:schemePreferred ?prefsSchemePreferred .
  } .

  # Thumbnail
  OPTIONAL { 
    ?uri gs:thumbnail ?icon .
    ?icon gs:contentUrl ?thumbnailUrl .
  } .

  FILTER (langMatches(lang(?title), "*") )
}

ORDER BY DESC(?dateCreated)
