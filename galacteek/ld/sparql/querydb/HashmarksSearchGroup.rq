PREFIX gs: <ips://galacteek.ld/>
PREFIX h: <ips://galacteek.ld/Hashmark#>

SELECT ?uri ?mimeType ?mimeCategory ?title
		 ?dateCreated ?descr ?kwords
		 ?thumbnailUrl {
{
		SELECT ?descr ?title ?mimeType ?mimeCategory ?dateCreated
					 ?thumbnailUrl ?iconUrl
					 ?uri (GROUP_CONCAT(?m) as ?kwords)
		WHERE {
				?uri a gs:Hashmark .

				OPTIONAL { ?uri h:dateCreated ?dateCreated . } .
				?uri h:title ?title .

				OPTIONAL {
					?uri h:keywordMatch ?m .
				} .

				OPTIONAL { ?uri h:description ?descr . } .
				OPTIONAL {
					?uri h:mimeCategory ?mimeCategory .
				} .

				OPTIONAL {
					?uri h:mimeType ?mimeType .
				}
				# Filter by MIME type if the MIME is set and the filter isn't empty
				FILTER ((!bound(?mimeType) && (STRLEN(?mimeCategoryQuery) = 0)) ||
								strStarts(?mimeType, str(?mimeCategoryQuery)))

				OPTIONAL {
					?uri gs:icon ?icon .
					?icon gs:contentUrl ?iconUrl .
				} .
				OPTIONAL {
					?uri gs:thumbnail ?thumbnail .
					?thumbnail gs:contentUrl ?thumbnailUrl .
				} .

				# title/description filter
				FILTER (contains(?title, ?searchQuery) ||
							  contains(?descr, ?searchQuery))

				FILTER (langMatches(lang(?title), str(?langTagMatch)) )
		}
		GROUP BY ?uri
		ORDER BY DESC(?dateCreated)
}
}
